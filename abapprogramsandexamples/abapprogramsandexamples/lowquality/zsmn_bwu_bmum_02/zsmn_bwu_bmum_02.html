<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<html>
<head>
<title>ZSMN_BWU_BMUM_02</title>
</head>
<body bgcolor="#FFFFE0">
<font size="3" face = "Arial" color="#000000"><b>Code listing for: ZSMN_BWU_BMUM_02</b></font>
<br>
<font size="3" face = "Arial" color="#000000"><b>Description:  Update (Insert or Delete) BPC Users</b></font>
<hr>
<pre width="100">
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*& Report  ZSMN_BWU_BMUM_02</font>
<font color ="#0000FF">*&</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*& Author:         Ross Robinson (104087)</font>
<font color ="#0000FF">*& Reference:      INC000002532627</font>
<font color ="#0000FF">*& Purpose:        Refer to SAPScript documentation.</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>

report zsmn_bwu_bmum_02 message-id zx_bwu_bmum.

data:
  lwa_file_table                   type file_table,
  lt_file_table                    type filetable,
  lv_rc                            type i,
  lv_data_rec                      type string,
  lt_data_tab                      type stringtab,
  lv_result                        type rs_bool.

types:
  begin of ty_wa_assign,
    user_id                        type uj_user_id,
    action                         type uj_action,
    rec_no                         type i,
  end of ty_wa_assign,
  ty_t_assign                      type table of ty_wa_assign with non-unique key user_id, team_id.

data:
  lwa_assign                       type ty_wa_assign,
  lt_assign                        type table of ty_wa_assign.

field-symbols:
  &lt;lt_assign&gt;                      type ty_t_assign.

data:
  lv_appset                        type uj_appset_id,
  lt_profs                         type uje_t_profile_action,
  lwa_profs                        like line of lt_profs,
  lwa_users                        type uje_s_user_action,
  lt_users                         type uje_t_user_action,
  lwa_user_id                      type uje_s_user_id,
  lt_user_id                       type uje_t_user_id,
  lv_action                        type uj_action.

data:
  lv_bname                         type xubname,
  lwa_return                       type bapiret2.

data:
  lv_idx                           type i.

data:
  lv_mtext                         type string,
  lwa_msg                          type bal_s_msg,
  lt_msg                           type rs_t_msg,
  lt_msg_hdr                       type rs_t_msg,
  lt_msg_dtl                       type rs_t_msg,
  lv_user_err                      type rs_bool,
  lv_acti_err                      type rs_bool,
  lv_crit_err                      type rs_bool,
  lv_pfix                          type string,
  lv_pct                           type i,
  lv_float                         type f.

selection-screen begin of block sel with frame title text-sel.

parameters:
  p_appset type uj_appset_id default 'COUNCIL'.

parameters:
  p_filenm type string lower case default 'C:\Data\Update Users.csv'.

selection-screen end of block sel.

selection-screen begin of block opt with frame title text-opt.

parameters:
  p_commit type rs_bool.

selection-screen end of block opt.

at selection-screen on value-request for p_filenm.

  clear:
    lt_file_table.

  "Call the file selection dialogue.
  call method cl_gui_frontend_services=&gt;file_open_dialog
    changing
      file_table              = lt_file_table
      rc                      = lv_rc
    exceptions
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      others                  = 5.
  if sy-subrc &lt;&gt; 0.
<font color ="#0000FF">*   Implement suitable error handling here</font>
  endif.

  "Read selected file.
  if lt_file_table is not initial.

    read table lt_file_table into lwa_file_table index 1.

    p_filenm = lwa_file_table-filename.

  endif.

at selection-screen on p_appset.

  clear:
    lv_appset.

  select single appset_id
    into lv_appset
    from uja_appset_info
    where appset_id = p_appset.

  if lv_appset is initial.
    message 'Invalid Environment' type 'E'.
  endif.

at selection-screen on p_filenm.

  clear:
    lv_result.

  call method cl_gui_frontend_services=&gt;file_exist
    exporting
      file                 = p_filenm
    receiving
      result               = lv_result
    exceptions
      cntl_error           = 1
      error_no_gui         = 2
      wrong_parameter      = 3
      not_supported_by_gui = 4
      others               = 5.
  if sy-subrc &lt;&gt; 0.
<font color ="#0000FF">* Implement suitable error handling here</font>
  endif.

  if lv_result &lt;&gt; 'X'.
    message 'File does not exist' type 'E'.
    return.
  endif.

at line-selection.

  "Offset the index.
  lv_idx = sy-curow - 2.

  "Retrieve message details.
  read table lt_msg into lwa_msg index lv_idx.
  if sy-subrc = 0.

    "Output message.
    message id lwa_msg-msgid
      type 'I'
      number lwa_msg-msgno
      with lwa_msg-msgv1
           lwa_msg-msgv2
           lwa_msg-msgv3
           lwa_msg-msgv4.

  endif.

start-of-selection.

  "Retrieve Team assignments to Users file.
  call method cl_gui_frontend_services=&gt;gui_upload
    exporting
      filename                = p_filenm
    changing
      data_tab                = lt_data_tab
    exceptions
      file_open_error         = 1
      file_read_error         = 2
      no_batch                = 3
      gui_refuse_filetransfer = 4
      invalid_type            = 5
      no_authority            = 6
      unknown_error           = 7
      bad_data_format         = 8
      header_not_allowed      = 9
      separator_not_allowed   = 10
      header_too_long         = 11
      unknown_dp_error        = 12
      access_denied           = 13
      dp_out_of_memory        = 14
      disk_full               = 15
      dp_timeout              = 16
      not_supported_by_gui    = 17
      error_no_gui            = 18
      others                  = 19.
  if sy-subrc &lt;&gt; 0.
    return.
<font color ="#0000FF">* Implement suitable error handling here</font>
  endif.

  "Retrieve User meta-data.
  call method cl_uje_user=&gt;get_all_user_id
    exporting
      i_appset_id = p_appset
    importing
      et_user_id  = lt_user_id.

  "Convert delimited text data into structured data.
  loop at lt_data_tab into lv_data_rec.

    split lv_data_rec
      at cl_abap_char_utilities=&gt;horizontal_tab
      into lwa_assign-user_id
           lwa_assign-action.

    append lwa_assign to lt_assign.

  endloop.

  "Validate table content.
  loop at lt_assign into lwa_assign.

    lv_idx = sy-tabix.

    lv_bname = lwa_assign-user_id.

    "Check the User exists.
    call function 'BAPI_USER_EXISTENCE_CHECK'
      exporting
        username = lv_bname
      importing
        return   = lwa_return.
    if lwa_return-number = 124.

      message w021 with lwa_assign-user_id lv_idx into lwa_msg-context-value.
      move-corresponding sy to lwa_msg.
      append lwa_msg to lt_msg_dtl.

      lv_user_err = 'X'.

    endif.

    "Check the Action inidicator is valid.
    if not ( lwa_assign-action = 'I' or lwa_assign-action = 'D' ).

      message w009 with lwa_assign-action lv_idx into lwa_msg-context-value.
      move-corresponding sy to lwa_msg.
      append lwa_msg to lt_msg_dtl.

      lv_acti_err = 'X'.

    endif.

  endloop.

  "Is there an error with the Users.
  if lv_user_err = 'X'.

    message e022 into lwa_msg-context-value.
    move-corresponding sy to lwa_msg.
    append lwa_msg to lt_msg_hdr.

    lv_crit_err = 'X'.

  endif.

  "If there is an error with the Action indicator.
  if lv_acti_err = 'X'.

    message e010 into lwa_msg-context-value.
    move-corresponding sy to lwa_msg.
    append lwa_msg to lt_msg_hdr.

    lv_crit_err = 'X'.

  endif.

  "If no critical errors are found.
  if lv_crit_err &lt;&gt; 'X'.

    message i023 into lwa_msg-context-value.
    move-corresponding sy to lwa_msg.
    append lwa_msg to lt_msg_hdr.

  endif.

  "Combine the messages.
  if lines( lt_msg_hdr ) &lt;&gt; 0.

    append lines of lt_msg_hdr to lt_msg.
    append initial line to lt_msg.
    append lines of lt_msg_dtl to lt_msg.

  else.
    lt_msg = lt_msg_dtl.
  endif.

  if lv_crit_err = 'X'.

    "Output errors before exit.
    loop at lt_msg into lwa_msg.

      case lwa_msg-msgty.
        when 'E'.
          lv_pfix = '@0A@'.
        when 'W'.
          lv_pfix = '@09@'.
        when 'I'.
          lv_pfix = '@08@'.
      endcase.

      if lwa_msg is initial.
        skip 1.
        continue.
      endif.

      lv_mtext = |{ lv_pfix } { lwa_msg-context-value }|.

      write: / lv_mtext hotspot.

    endloop.

    "Exit on errors.
    return.

  endif.

  "Validate duplicated entries.
  loop at lt_assign into lwa_assign.
  endloop.

  "Only proceed if the configuration should be updated.
  if p_commit = 'X'.

    "Process all Users.
    loop at lt_assign into lwa_assign.

      lv_idx = sy-tabix.

      clear:
        lt_user_id.

      append lwa_assign-user_id to lt_user_id.

      "Calculate progress.
      lv_float = ( lv_idx / lines( lt_assign ) ) * 100.
      lv_pct = lv_float.

      "Define progress message.
      lv_mtext = |Updating Environment User { lwa_assign-user_id } ({ lv_pct }%)|.

      "Output progress message.
      call function 'SAPGUI_PROGRESS_INDICATOR'
        exporting
          percentage = lv_pct
          text       = lv_mtext.

      case lwa_assign-action.
        when 'D'.

          "Delete User.
          try.

              call method cl_uje_user=&gt;delete_users
                exporting
                  i_appset_id = p_appset
                  it_users    = lt_user_id.

            catch cx_uj_no_auth .
            catch cx_uje_exception .
          endtry.

        when 'I'.

          "Add User.
          try.

              call method cl_uje_user=&gt;add_users
                exporting
                  i_appset_id = p_appset
                  it_users    = lt_user_id.

            catch cx_uj_no_auth .
            catch cx_uje_exception .
          endtry.

      endcase.

      case lwa_assign-action.
        when 'D'.
          message i024 with lwa_assign-user_id into lwa_msg-context-value.
        when 'I'.
          message i025 with lwa_assign-user_id into lwa_msg-context-value.
      endcase.

      move-corresponding sy to lwa_msg.
      append lwa_msg to lt_msg_dtl.

    endloop.

  endif.

  append lines of lt_msg_dtl to lt_msg.

  "Output messages before exit.
  loop at lt_msg into lwa_msg.

    case lwa_msg-msgty.
      when 'E'.
        lv_pfix = '@0A@'.
      when 'W'.
        lv_pfix = '@09@'.
      when 'I'.
        lv_pfix = '@08@'.
    endcase.

    if lwa_msg is initial.
      skip 1.
      continue.
    endif.

    if lwa_msg-context-value(1) = '@'.
      lv_mtext = |{ lwa_msg-context-value }|.
    else.
      lv_mtext = |{ lv_pfix } { lwa_msg-context-value }|.
    endif.

    write: / lv_mtext hotspot.

  endloop.

<font color ="#0000FF">*Text elements</font>
<font color ="#0000FF">*----------------------------------------------------------</font>
<font color ="#0000FF">* OPT Options</font>
<font color ="#0000FF">* SEL Selection</font>


<font color ="#0000FF">*Selection texts</font>
<font color ="#0000FF">*----------------------------------------------------------</font>
<font color ="#0000FF">* P_APPSET D       .</font>
<font color ="#0000FF">* P_COMMIT         Commit</font>
<font color ="#0000FF">* P_FILENM         File</font>


<font color ="#0000FF">*Messages</font>
<font color ="#0000FF">*----------------------------------------------------------</font>
<font color ="#0000FF">*</font>
<font color ="#0000FF">* Message class: Hard coded</font>
<font color ="#0000FF">*   Invalid Environment</font>
<font color ="#0000FF">*</font>
<font color ="#0000FF">* Message class: LWA_MSG-MSGID</font>
<font color ="#0000FF">*LWA</font>
<font color ="#0000FF">*</font>
<font color ="#0000FF">* Message class: ZX_BWU_BMUM</font>
<font color ="#0000FF">*009   Invalid Action '&1' at line '&2'.</font>
<font color ="#0000FF">*010   Invalid Action(s) in file.</font>
<font color ="#0000FF">*021   Invalid User '&1' at line '&2'.</font>
<font color ="#0000FF">*022   Invalid User(s) in file.</font>
<font color ="#0000FF">*023   No errors found in User creation/deletion</font>
<font color ="#0000FF">*024   @18@ User '&1' removed from Environment</font>
<font color ="#0000FF">*025   @17@ User '&1' added to Environment</font>
</pre>
<hr>
<font size="2" face = "Sans Serif">Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 731
</font>
</body>
</html>
