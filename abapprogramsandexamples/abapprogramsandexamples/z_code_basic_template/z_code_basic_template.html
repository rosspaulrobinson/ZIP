<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<html>
<head>
<title>Z_CODE_BASIC_TEMPLATE</title>
</head>
<body bgcolor="#FFFFE0">
<font size="3" face = "Arial" color="#000000"><b>Code listing for: Z_CODE_BASIC_TEMPLATE</b></font>
<br>
<font size="3" face = "Arial" color="#000000"><b>Description:  Demo code to be used as basic template.</b></font>
<hr>
<pre width="100">
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*& Report  Z_CODE_BASIC_TEMPLATE</font>
<font color ="#0000FF">*&</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*&</font>
<font color ="#0000FF">*& Basic example of an executable program using a local class. This</font>
<font color ="#0000FF">*& example demonstrates the use of a number of key selection-screen</font>
<font color ="#0000FF">*& events and a technique for passing selection-screen parameters to</font>
<font color ="#0000FF">*& the local class.</font>
<font color ="#0000FF">*&</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
report z_code_basic_template.
<font color ="#0000FF">*----------------------------------------------------------------------*</font>
<font color ="#0000FF">*       CLASS lcl DEFINITION</font>
<font color ="#0000FF">*----------------------------------------------------------------------*</font>
<font color ="#0000FF">*</font>
<font color ="#0000FF">*----------------------------------------------------------------------*</font>
class lcl definition.

  public section.

    types:
      begin of ty_wa_param,
        p_bname                    type xubname,
        s_bname                    type range of xubname,
        p_selp                     type rs_bool,
        p_sels                     type rs_bool,
      end of ty_wa_param.

    class-data:
      wa_param                     type ty_wa_param.

    class-methods factory
      importing
        iwa_param type ty_wa_param
      exporting
        er_lcl    type ref to lcl.

    methods execute.

  private section.

    data:
      t_usr02                      type table of usr02.

    methods db_read_data.

endclass.                    "lcl DEFINITION
<font color ="#0000FF">*----------------------------------------------------------------------*</font>
<font color ="#0000FF">*       CLASS lcl IMPLEMENTATION</font>
<font color ="#0000FF">*----------------------------------------------------------------------*</font>
<font color ="#0000FF">*</font>
<font color ="#0000FF">*----------------------------------------------------------------------*</font>
class lcl implementation.

  method factory.

    "Create instance of class.
    create object er_lcl type lcl.

    "Persist import parameter data.
    er_lcl-&gt;wa_param = iwa_param.

  endmethod.                    "factory

  method execute.

    "Retrieve data from the database.
    db_read_data( ).

  endmethod.                    "execute

  method db_read_data.

    data:
      lv_mtext                     type string.

    if wa_param-p_selp = rs_c_true.

      "Retrieve data using select-options values.
      select *
        into table t_usr02
        from usr02
        where bname = wa_param-p_bname.

    else.

      "Retrieve data using single value selection.
      select *
        into table t_usr02
        from usr02
        where bname in wa_param-s_bname.

    endif.

    lv_mtext = |{ lines( t_usr02 ) } records retrieved from table USR02.|.
    write:/ lv_mtext.

  endmethod.                    "db_read_data

endclass.                    "lcl IMPLEMENTATION

selection-screen begin of block b1 with frame title _title_1.
parameters p_bname like lcl=&gt;wa_param-p_bname.
select-options s_bname for lcl=&gt;wa_param-p_bname.
selection-screen end of block b1.

selection-screen begin of block b2 with frame title _title_2.
parameters p_selp type rs_bool radiobutton group rg1 default 'X' user-command uc1.
parameters p_sels type rs_bool radiobutton group rg1.
selection-screen end of block b2.

initialization.

  "Set initial display as required.

  _title_1 = 'Selection'.
  _title_2 = 'Options'.

  %_p_bname_%_app_%-text = 'User name'.
  %_s_bname_%_app_%-text = 'User name'.

  %_p_selp_%_app_%-text = 'Select single'.
  %_p_sels_%_app_%-text = 'Select multiple'.

  p_bname = '104087'.

at selection-screen output.

  "Alter display as required.

  loop at screen.

    if screen-name cp '*P_BNAME*'.
      if p_selp = rs_c_true.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    endif.

    if screen-name cp '*S_BNAME*'.
      if p_sels = rs_c_true.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    endif.

  endloop.

at selection-screen on p_bname.

  if p_selp = rs_c_true and p_bname is initial.
    message 'Field value is obligatory' type 'S' display like 'W'.
  endif.

at selection-screen on s_bname.

  if p_sels = rs_c_true and s_bname[] is initial.
    message 'Field value is obligatory' type 'S' display like 'W'.
  endif.

start-of-selection.

  data:
    lr_lcl                         type ref to lcl,
    lwa_param                      type lcl=&gt;ty_wa_param.

  "Transfer selection-screen parameters to a work area.
  lwa_param-p_bname = p_bname.
  lwa_param-s_bname = s_bname[].
  lwa_param-p_selp  = p_selp.
  lwa_param-p_sels  = p_sels.

  "Create instance of class.
  call method lcl=&gt;factory
    exporting
      iwa_param = lwa_param
    importing
      er_lcl    = lr_lcl.

  "Execute class.
  lr_lcl-&gt;execute( ).


<font color ="#0000FF">*Messages</font>
<font color ="#0000FF">*----------------------------------------------------------</font>
<font color ="#0000FF">*</font>
<font color ="#0000FF">* Message class: Hard coded</font>
<font color ="#0000FF">*   Field value is obligatory</font>
</pre>
<hr>
<font size="2" face = "Sans Serif">Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 731
</font>
</body>
</html>
