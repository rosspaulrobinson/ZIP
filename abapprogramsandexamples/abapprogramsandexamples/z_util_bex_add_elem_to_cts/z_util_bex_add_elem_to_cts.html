<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<html>
<head>
<title>Z_UTIL_BEX_ADD_ELEM_TO_CTS</title>
</head>
<body bgcolor="#FFFFE0">
<font size="3" face = "Arial" color="#000000"><b>Code listing for: Z_UTIL_BEX_ADD_ELEM_TO_CTS</b></font>
<br>
<font size="3" face = "Arial" color="#000000"><b>Description:  Add BEx Elements to Transport Request/Task</b></font>
<hr>
<pre width="100">
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*& Report  Z_104087_BEX_ELEM_TO_TRANSPORT</font>
<font color ="#0000FF">*&</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*&</font>
<font color ="#0000FF">*&</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>

report z_util_bex_add_elem_to_cts.

load-of-program.

  tables:
    rszcompdir,
    rszeltdir.

  data:
    lv_obj_name                    type sobj_name,
    lt_obj_name                    type table of sobj_name,
    lwa_new_gtadir_entry           type gtadir,
    lwa_new_tadir_entry            type tadir,
    lwa_e070                       type e070,
    lwa_e070_var                   type e070,
    lwa_e071                       type e071,
    lt_e071                        type table of e071,
    lt_e071_var                    type table of e071,
    lv_as4pos                      type ddposition,
    lv_as4pos_var                  type ddposition,
    lv_uid                         type sysuuid_25,
    lt_dv_compuid                  type table of sysuuid_25,
    lt_dv_eltuid                   type table of sysuuid_25,
    lt_dv_uid                      type table of sysuuid_25,
    lt_dv_uid_ref                  type table of sysuuid_25,
    lth_eltdir                     type hashed table of rszeltdir with unique key eltuid,
    lv_lines_before                type i,
    lv_lines_after                 type i,
    lv_index                       type i,
    lv_mtext                       type string,
    lv_elem_type                   type c length 6.

  field-symbols:
    &lt;lwa_eltdir&gt;                   type rszeltdir.


  selection-screen begin of block b1 with frame title text-tsk.

  parameters     trkorr   type trkorr.
  parameters     ctsvar   type trkorr.

  selection-screen end of block b1.

  selection-screen begin of block b2 with frame title text-tnm.

  select-options compid   for rszcompdir-compid.

  selection-screen end of block b2.

  selection-screen begin of block b3 with frame title text-uid.

  select-options eltuid   for rszeltdir-eltuid.

  selection-screen end of block b3.

  selection-screen begin of block b4 with frame title text-flt.

  parameters     getvar   type rs_bool default ' ' user-command getvar.
  parameters     getstr   type rs_bool default 'X'.
  parameters     getrkf   type rs_bool default 'X'.
  parameters     getckf   type rs_bool default 'X'.

  selection-screen end of block b4.

  selection-screen begin of block b5 with frame title text-opt.

  parameters     derive   type rs_bool.
  parameters     setpack  type rs_bool user-command setpack.
  parameters     package  type devclass default 'ZBW'.
  parameters     simulat  type rs_bool default 'X'.

  selection-screen end of block b5.

at selection-screen on trkorr.

  "Check the transport exists.
  select single *
    into lwa_e070
    from e070
    where trkorr = trkorr.
  if sy-subrc &lt;&gt; 0.
    message 'Transport Request/Task does not exist.' type 'E'.
  endif.

  "Check the transport status.
  if lwa_e070-trstatus &lt;&gt; 'D'.
    message 'Transport Request/Task is not modifiable.' type 'E'.
  endif.

at selection-screen on ctsvar.

  if getvar = rs_c_true and ctsvar is not initial.

    "Check the transport exists.
    select single *
      into lwa_e070_var
      from e070
      where trkorr = ctsvar.
    if sy-subrc &lt;&gt; 0.
      message 'Transport Request/Task does not exist.' type 'E'.
    endif.

    "Check the transport status.
    if lwa_e070_var-trstatus &lt;&gt; 'D'.
      message 'Transport Request/Task is not modifiable.' type 'E'.
    endif.

  endif.

at selection-screen on compid.

  if eltuid[] is initial and compid[] is initial.
    message 'A selection on either the Technical name or Element ID is required.' type 'E'.
  endif.

at selection-screen on eltuid.

  if eltuid[] is initial and compid[] is initial.
    message 'A selection on either the Technical name or Element ID is required.' type 'E'.
  endif.

at selection-screen output.

  loop at screen.

    if screen-name cp '*PACKAGE*'.

      if setpack = rs_c_true.
        screen-input = 1.
      else.
        screen-input = 0.
      endif.

    endif.

    if screen-name cp '*CTSVAR*'.

      if getvar = rs_c_true.
        screen-input = 1.
      else.
        screen-input = 0.
      endif.

    endif.

    modify screen.

  endloop.

start-of-selection.

  if simulat = rs_c_true.
    write: / 'SIMULATION MODE!'. skip 1.
  endif.

  "Retrieve the elements by Technical name.
  if compid is not initial.

    select compuid
      into table lt_dv_compuid
      from rszcompdir
      where compid in compid
      group by compuid.

  endif.

  "Retrieve the elements by Unique ID.
  if eltuid is not initial.

    select eltuid
      into table lt_dv_eltuid
      from rszeltdir
      where eltuid in eltuid
      group by eltuid.

  endif.

  "Combine the elements retrieved by Technical name and by Element ID.
  append lines of lt_dv_compuid to lt_dv_uid.
  append lines of lt_dv_eltuid  to lt_dv_uid.

  "Check if any elements meet the selection criteria.
  if lt_dv_uid is initial.

    message 'No query elements found for the selection.' type 'S' display like 'W'.
    return.

  endif.

  "Display number of lines selected.
  lv_mtext = |{ lines( lt_dv_uid ) } Elements found based on the selection.|.
  write: / lv_mtext.

  "Collect elements that are referenced by the selected elements.
  if derive = rs_c_true.

    do 10 times.

      "Store the index.
      lv_index = sy-index.

      "Get the number of elements collected before the reference check.
      lv_lines_before = lines( lt_dv_uid ).

      "Retrieve the elements referenced by the selected/collected elements.
      select teltuid
        into table lt_dv_uid_ref
        from rszeltxref
        for all entries in lt_dv_uid
        where seltuid = lt_dv_uid-table_line and
              objvers = 'A'
        group by teltuid.

      "Combine the referenced elements.
      append lines of lt_dv_uid_ref to lt_dv_uid.

      "Remove duplicates.
      sort lt_dv_uid.
      delete adjacent duplicates from lt_dv_uid.

      "Get the number of elements collected after the reference check.
      lv_lines_after = lines( lt_dv_uid ).

      "If the number of elements collected has not increased exit the lookup.
      if lv_lines_before = lv_lines_after.
        exit.
      endif.

    enddo.

    if lv_index = 10.
      message 'The search for cross-referenced elements may not have completed.' type 'W'.
    endif.

    lv_mtext = |{ lines( lt_dv_uid ) } Elements collected based on Element cross-reference.|.
    write: / lv_mtext.

  endif.

  "Retrieve meta-data for Elements.
  if lt_dv_uid is not initial.

    select
        eltuid
        deftp
        subdeftp
        reusable
      into corresponding fields of table lth_eltdir  ##TOO_MANY_ITAB_FIELDS
      from rszeltdir
      for all entries in lt_dv_uid
      where eltuid = lt_dv_uid-table_line and
            objvers = 'A'.

  endif.

  "Loop through the elements and transfer to the development object name format.
  loop at lt_dv_uid into lv_uid.

    lv_obj_name = lv_uid.
    append lv_obj_name to lt_obj_name.

  endloop.

  "Retrieve any development objects in the selection that do not have a package assignment.
  select obj_name
    into table lt_obj_name
    from tadir
    for all entries in lt_obj_name
    where pgmid    = 'R3TR' and
          object   = 'ELEM' and
          obj_name = lt_obj_name-table_line and
          devclass = '$TMP'.
  if sy-subrc &lt;&gt; 0.
    clear lt_obj_name.
  endif.

  if lt_obj_name is not initial and setpack is initial.

    lv_mtext = |{ lines( lt_obj_name ) } Elements found with package '$TMP'; adjust in TCode SE03 after collection.|.
    write: / lv_mtext.

  endif.

  "Change the package assignment.
  if simulat = rs_c_false.

    loop at lt_obj_name into lv_obj_name.

      call function 'TR_TADIR_INTERFACE'
        exporting
          wi_tadir_pgmid                 = 'R3TR'
          wi_tadir_object                = 'ELEM'
          wi_tadir_obj_name              = lv_obj_name
          wi_tadir_devclass              = 'ZBW'
        exceptions
          tadir_entry_not_existing       = 1
          tadir_entry_ill_type           = 2
          no_systemname                  = 3
          no_systemtype                  = 4
          original_system_conflict       = 5
          object_reserved_for_devclass   = 6
          object_exists_global           = 7
          object_exists_local            = 8
          object_is_distributed          = 9
          obj_specification_not_unique   = 10
          no_authorization_to_delete     = 11
          devclass_not_existing          = 12
          simultanious_set_remove_repair = 13
          order_missing                  = 14
          no_modification_of_head_syst   = 15
          pgmid_object_not_allowed       = 16
          masterlanguage_not_specified   = 17
          devclass_not_specified         = 18
          specify_owner_unique           = 19
          loc_priv_objs_no_repair        = 20
          gtadir_not_reached             = 21
          object_locked_for_order        = 22
          change_of_class_not_allowed    = 23
          no_change_from_sap_to_tmp      = 24
          others                         = 25.
      if sy-subrc &lt;&gt; 0.

        lv_mtext = |Unmanaged error occured changing the package for element { lv_obj_name }.|.
        message lv_mtext type 'S' display like 'E'.
        continue.

      endif.

    endloop.

  endif.

  "Retrieve the highest sequence number for the request/task.
  select single max( as4pos )
    into lv_as4pos
    from e071
    where trkorr = trkorr.

  "Retrieve the contents of the request/task.
  select *
    into table lt_e071
    from e071
    where trkorr = trkorr
    order by as4pos ascending.

  if ctsvar is not initial.

    "Retrieve the highest sequence number for the request/task.
    select single max( as4pos )
      into lv_as4pos_var
      from e071
      where trkorr = ctsvar.

    "Retrieve the contents of the request/task.
    select *
      into table lt_e071_var
      from e071
      where trkorr = ctsvar
      order by as4pos ascending.

  endif.

  "Loop through the elements and transfer to the transport structure.
  loop at lt_dv_uid into lv_uid.

    "Check the options for object inclusion.
    read table lth_eltdir assigning &lt;lwa_eltdir&gt;
      with table key
        eltuid = lv_uid.
    if sy-subrc = 0.

      lv_elem_type = |{ &lt;lwa_eltdir&gt;-deftp }{ &lt;lwa_eltdir&gt;-subdeftp }|.

      case lv_elem_type.
        when 'VAR' or 'VARVAR'.
          check getvar = rs_c_true.
        when 'STRSTR'.
          check getstr = rs_c_true.
        when 'SELRKF'.
          check getrkf = rs_c_true.
        when 'CKFCKF'.
          check getckf = rs_c_true.
        when others.
          sy-subrc = 0.
      endcase.

    endif.

    if ctsvar is not initial and ( lv_elem_type = 'VAR' or lv_elem_type = 'VARVAR' ).

      add 1 to lv_as4pos_var.

      "Add the Variable Elements to the transport.
      lwa_e071-trkorr   = ctsvar.
      lwa_e071-as4pos   = lv_as4pos.
      lwa_e071-pgmid    = 'R3TR'.
      lwa_e071-object   = 'ELEM'.
      lwa_e071-obj_name = lv_uid.
      append lwa_e071 to lt_e071_var.

    else.

      add 1 to lv_as4pos.

      "Add the Elements to the transport.
      lwa_e071-trkorr   = trkorr.
      lwa_e071-as4pos   = lv_as4pos.
      lwa_e071-pgmid    = 'R3TR'.
      lwa_e071-object   = 'ELEM'.
      lwa_e071-obj_name = lv_uid.
      append lwa_e071 to lt_e071.

    endif.

  endloop.

  if lt_e071_var is not initial.
    lv_mtext = |{ lines( lt_e071_var ) } Variable Elements transfered to request { ctsvar }.|.
    write: / lv_mtext.
  endif.

  if lt_e071 is not initial.
    lv_mtext = |{ lines( lt_e071 ) } Elements transfered to request { trkorr }.|.
    write: / lv_mtext.
  endif.

  if simulat = rs_c_false.

    "Transfer variables.
    if ctsvar is not initial.

      call function 'TRINT_MODIFY_COMM'
        exporting
          wi_e070                        = lwa_e070_var
          wi_sel_e071                    = 'X'
        tables
          wt_e071                        = lt_e071_var
        exceptions
          chosen_project_closed          = 1
          e070_insert_error              = 2
          e070_update_error              = 3
          e071k_insert_error             = 4
          e071k_update_error             = 5
          e071_insert_error              = 6
          e071_update_error              = 7
          e07t_insert_error              = 8
          e07t_update_error              = 9
          e070c_insert_error             = 10
          e070c_update_error             = 11
          locked_entries                 = 12
          locked_object_not_deleted      = 13
          ordername_forbidden            = 14
          order_change_but_locked_object = 15
          order_released                 = 16
          order_user_locked              = 17
          tr_check_keysyntax_error       = 18
          no_authorization               = 19
          wrong_client                   = 20
          unallowed_source_client        = 21
          unallowed_user                 = 22
          unallowed_trfunction           = 23
          unallowed_trstatus             = 24
          no_systemname                  = 25
          no_systemtype                  = 26
          others                         = 27.
      if sy-subrc &lt;&gt; 0.

        message 'Unmanaged error occured adding objects to request.' type 'S' display like 'E'.

      endif.

    endif.

    "Transfer elements.
    call function 'TRINT_MODIFY_COMM'
      exporting
        wi_e070                        = lwa_e070
        wi_sel_e071                    = 'X'
      tables
        wt_e071                        = lt_e071
      exceptions
        chosen_project_closed          = 1
        e070_insert_error              = 2
        e070_update_error              = 3
        e071k_insert_error             = 4
        e071k_update_error             = 5
        e071_insert_error              = 6
        e071_update_error              = 7
        e07t_insert_error              = 8
        e07t_update_error              = 9
        e070c_insert_error             = 10
        e070c_update_error             = 11
        locked_entries                 = 12
        locked_object_not_deleted      = 13
        ordername_forbidden            = 14
        order_change_but_locked_object = 15
        order_released                 = 16
        order_user_locked              = 17
        tr_check_keysyntax_error       = 18
        no_authorization               = 19
        wrong_client                   = 20
        unallowed_source_client        = 21
        unallowed_user                 = 22
        unallowed_trfunction           = 23
        unallowed_trstatus             = 24
        no_systemname                  = 25
        no_systemtype                  = 26
        others                         = 27.
    if sy-subrc &lt;&gt; 0.

      message 'Unmanaged error occured adding objects to request.' type 'S' display like 'E'.

    endif.

  endif.

<font color ="#0000FF">*Text elements</font>
<font color ="#0000FF">*----------------------------------------------------------</font>
<font color ="#0000FF">* FLT Inclusion of Re-usable Elements</font>
<font color ="#0000FF">* OPT Options</font>
<font color ="#0000FF">* TNM Select Elements by Technical name</font>
<font color ="#0000FF">* TSK Request/Task</font>
<font color ="#0000FF">* UID Select Elements by Unique ID</font>


<font color ="#0000FF">*Selection texts</font>
<font color ="#0000FF">*----------------------------------------------------------</font>
<font color ="#0000FF">* COMPID         Technical name</font>
<font color ="#0000FF">* CTSVAR         Variable Elements</font>
<font color ="#0000FF">* DERIVE         Collect referenced Elements</font>
<font color ="#0000FF">* ELTUID         Unique ID</font>
<font color ="#0000FF">* GETCKF         Calculated Key Figures</font>
<font color ="#0000FF">* GETRKF         Restricted Key Figures</font>
<font color ="#0000FF">* GETSTR         Structure</font>
<font color ="#0000FF">* GETVAR         Variables</font>
<font color ="#0000FF">* PACKAGE         Change Package to</font>
<font color ="#0000FF">* SETPACK         Set Package if initial</font>
<font color ="#0000FF">* SIMULAT         Simulate only</font>
<font color ="#0000FF">* TRKORR         All Element types</font>


<font color ="#0000FF">*Messages</font>
<font color ="#0000FF">*----------------------------------------------------------</font>
<font color ="#0000FF">*YPE</font>
<font color ="#0000FF">*</font>
<font color ="#0000FF">* Message class: Hard coded</font>
<font color ="#0000FF">*   Transport Request/Task does not exist.</font>
</pre>
<hr>
<font size="2" face = "Sans Serif">Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 731
</font>
</body>
</html>
