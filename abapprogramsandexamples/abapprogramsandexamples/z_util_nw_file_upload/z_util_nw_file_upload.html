<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<html>
<head>
<title>Z_UTIL_NW_FILE_UPLOAD</title>
</head>
<body bgcolor="#FFFFE0">
<font size="3" face = "Arial" color="#000000"><b>Code listing for: Z_UTIL_NW_FILE_UPLOAD</b></font>
<br>
<font size="3" face = "Arial" color="#000000"><b>Description:  Upload files to Application Server</b></font>
<hr>
<pre width="100">
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*& Report  Z_UTIL_NW_FILE_UPLOAD</font>
<font color ="#0000FF">*&</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*&</font>
<font color ="#0000FF">*&</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>

report z_util_nw_file_upload.


selection-screen begin of block src with frame title t_src.
parameters p_folder type c length 200 lower case default 'C:\data\'.
selection-screen end of block src.

selection-screen begin of block sel with frame title t_sel.
parameters p_regex type string lower case default '.*(PMCO).*(.bin|.txt)'.
selection-screen end of block sel.

selection-screen begin of block tgt with frame title t_tgt.
parameters p_dir type c length 200 lower case default '/usr/sap/trans/temp/DEV/DRA'.
selection-screen end of block tgt.

selection-screen begin of block opt with frame title t_opt.
parameters p_commit type rs_bool default ''.
parameters p_joblog type rs_bool default 'X'.
selection-screen end of block opt.


initialization.

  t_src = 'Source on Workstation'.
  t_sel = 'Filter files using Regular Expression'.
  t_tgt = 'Target on Application Server'.
  t_opt = 'Options'.

start-of-selection.

  data:
    lv_dir                           type c length 200,
    lv_regex                         type string,
    lv_folder                        type c length 200,
    lv_commit                        type rs_bool,
    lv_joblog                        type rs_bool.

  data:
    lwa_file                         type file_info,
    lt_file                          type table of file_info,
    lv_file                          type pfeflname,
    lt_dv_file                       type table of pfeflname,
    lv_count                         type i.

  data:
    lv_path                          type saepfad,
    lt_path                          type table of saepfad,
    lv_targetpath                    type saepfad.

  data:
    lv_mtext                         type string,
    lt_mtext                         type stringtab.

  field-symbols:
    &lt;lv_param&gt;                       type any.

  assign ('P_DIR') to &lt;lv_param&gt;.
  if sy-subrc = 0.
    lv_dir = &lt;lv_param&gt;.
  endif.

  assign ('P_REGEX') to &lt;lv_param&gt;.
  if sy-subrc = 0.
    lv_regex = &lt;lv_param&gt;.
  endif.

  assign ('P_FOLDER') to &lt;lv_param&gt;.
  if sy-subrc = 0.
    lv_folder = &lt;lv_param&gt;.
  endif.

  assign ('P_COMMIT') to &lt;lv_param&gt;.
  if sy-subrc = 0.
    lv_commit = &lt;lv_param&gt;.
  endif.

  assign ('P_JOBLOG') to &lt;lv_param&gt;.
  if sy-subrc = 0.
    lv_joblog = &lt;lv_param&gt;.
  endif.

  if lv_folder is initial or
    lv_regex is initial or
    lv_dir is initial.

    lv_folder = 'C:\data\'.
    lv_regex  = '.*(PMCO).*(.bin|.txt)'.
    lv_dir    = '/usr/sap/trans/temp'.
    lv_commit = 'X'.
    lv_joblog = 'X'.

  endif.

  call method cl_gui_frontend_services=&gt;directory_list_files
    exporting
      directory                   = |{ lv_folder }|
<font color ="#0000FF">*     filter                      = '*.*'</font>
      files_only                  = rs_c_true
<font color ="#0000FF">*     directories_only            =</font>
    changing
      file_table                  = lt_file
      count                       = lv_count
    exceptions
      cntl_error                  = 1
      directory_list_files_failed = 2
      wrong_parameter             = 3
      error_no_gui                = 4
      not_supported_by_gui        = 5
      others                      = 6.
  if sy-subrc &lt;&gt; 0.
    return.
  endif.

  "Collect files in the selection.
  loop at lt_file into lwa_file.

    "Match the pattern.
    find regex lv_regex in lwa_file-filename.
    if sy-subrc = 0.

      "Collect the file.
      lv_file = lwa_file-filename.
      append lv_file to lt_dv_file.

    endif.

  endloop.

  if lv_joblog = rs_c_true.

    lv_mtext = |Directory contains { lines( lt_file ) } files.|.
    write: / lv_mtext.

    lv_mtext = |{ lines( lt_dv_file ) } files match the REGEX filter.|.
    write: / lv_mtext.

    lv_mtext = 'The following files will be uploaded.'.
    write: / lv_mtext.

    skip.

    loop at lt_dv_file into lv_file.

      lv_mtext = lv_file.
      write: / lv_mtext.

    endloop.

  endif.

  "Loop through files.
  loop at lt_dv_file into lv_file.

    "Define the path.
    lv_path = lv_folder && lv_file.

    "Define the target path.
    lv_targetpath = lv_dir && lv_file.

    if lv_commit = rs_c_true.

      "Upload files.
      call function 'ARCHIVFILE_CLIENT_TO_SERVER'
        exporting
          path             = lv_path
          targetpath       = lv_targetpath
        exceptions
          error_file       = 1
          no_authorization = 2
          others           = 3.
      if sy-subrc &lt;&gt; 0.
        continue.
      endif.

    endif.

  endloop.

  return.


<font color ="#0000FF">*Selection texts</font>
<font color ="#0000FF">*----------------------------------------------------------</font>
<font color ="#0000FF">* P_COMMIT         Confirm file transfer</font>
<font color ="#0000FF">* P_DIR         Directory</font>
<font color ="#0000FF">* P_FOLDER         Folder</font>
<font color ="#0000FF">* P_JOBLOG         Write Job Log</font>
<font color ="#0000FF">* P_REGEX         Filter (REGEX)</font>
</pre>
<hr>
<font size="2" face = "Sans Serif">Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 731
</font>
</body>
</html>
